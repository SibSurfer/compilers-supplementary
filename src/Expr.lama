-- Expression evaluator

import List;
import State;
import World;

-- As association map which maps "\otimes" into "\oplus"
var ops = {
  ["+" , infix + ],
  ["-" , infix - ],
  ["*" , infix * ],
  ["/" , infix / ],
  ["%" , infix % ],
  ["==", infix ==],
  ["!=", infix !=],
  ["<" , infix < ],
  ["<=", infix <=],
  [">" , infix > ],
  [">=", infix >=],
  ["&&", infix &&],
  ["!!", infix !!]
};

-- The evaluator for infix operators: takes an operator as a string
-- and two operand values, and returns the result
public fun evalOp (op, l, r) {
  case assoc (ops, op) of
    Some (f) -> f (l, r)
  esac
}

-- Evaluates an expression "expr" in a configuration "c".
-- A configuration is a pair of a state "s" and a world "w".
-- Returns a final configuration (if any)
--
-- A expression is represented by a data structure of the following shape:
--
-- expr = Assn    (expr, expr)         |
--        Seq     (expr, expr)         |
--        Skip                         |
--        Read    (string)             |
--        Write   (expr)               |
--        If      (expr, expr, expr)   |
--        While   (expr, expr)         |
--        DoWhile (expr, expr)         |
--        Var     (string)             |
--        Ref     (string)             |
--        Const   (int)                |
--        Binop   (string, expr, expr) |
--        Ignore  (expr)

-- Evaluates a list of expressions, properly threading a configurations.
-- Returns the final configuration and the list of values
fun evalList (c, exprs) {
  case foldl (fun ([c, vals], e) {
                case eval (c, e) of
                  [c, v] -> [c, v : vals]
                esac
              },
              [c, {}],
              exprs) of
    [c, vals] -> [c, reverse (vals)]
  esac
}

fun eval(c@[st, w], expr) {
  case expr of
      Assn (fst, snd) -> 
        case evalList(c, { fst, snd }) of
        [c@[st, w], { Ref(n), Val(v) }] -> [[st <- [n, v], w], Val(v)]
      esac
    | Seq (fst, snd) -> case evalList(c, { fst, snd }) of
      [c, { g, v }] -> [c, v]
    esac
    | Skip -> [c, Void]
    | Read (n) -> case readWorld(w) of
      [v, t] -> [[st <- [n, v], t], Void]
    esac
    | Write (ex) -> case eval(c, ex) of
      [[st, w], Val(v)] -> [[st, writeWorld(v, w)], Void]
    esac
    | If(st, iff, elsse)    -> case eval(c, st) of
        [c, Val(0)] -> eval(c, elsse)
      | [c, Val(g)] -> eval(c, iff)
    esac
    | cycle@(While (st, exBody)) -> case eval(c, st) of
        [c, Val(0)] -> [c, Void]
      | [c, Val(g)] -> case eval(c, exBody) of
        [c, g] -> eval(c, cycle)
      esac
    esac
    | DoWhile (exBody, st) -> case eval(c, exBody) of
      [c, g] -> eval(c, While(st, exBody))
    esac
    | Binop (op, fst, snd) -> case evalList(c, { fst, snd }) of
      [cc, { Val(a), Val(b) }] -> [cc, Val(evalOp(op, a, b))]
    esac
    | Var (x) -> [c, Val(st(x))]
    | Ref (x)-> [c, Ref(x)]
    | Const (n)-> [c, Val(n)]
    
    | Ignore (ex)-> case eval(c, ex) of
      [c, g] -> [c, Void]
    esac
  esac
}


-- Evaluates a program with a given input and returns an output
public fun evalExpr (input, expr) {
  case eval ([emptyState, createWorld (input)], expr) of
    [c, _] -> c.snd.getOutput
  esac
}
