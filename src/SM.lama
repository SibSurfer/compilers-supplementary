-- Stack machine.

import Array;
import List;
import Fun;
import Collection;
import World;
import State;
import Expr;
import Buffer;

-- Stack code printer. Takes a list of SM instructions, return its
-- string representation.
public fun showSMInsn (i) {
  case i of
    READ         -> sprintf ("READ")
  | WRITE        -> sprintf ("WRITE")
  | BINOP (s)    -> sprintf ("BINOP %s", s)
  | LD    (x)    -> sprintf ("LD %s", x)
  | ST    (x)    -> sprintf ("ST %s", x)
  | CONST (n)    -> sprintf ("CONST %d", n)
  | LABEL (s)    -> sprintf ("LABEL %s", s)
  | JMP   (l)    -> sprintf ("JMP %s", l)
  | CJMP  (c, l) -> sprintf ("CJMP %s, %s", c, l)
  esac
}

public fun showSM (prg) {
  map (fun (i) {showSMInsn (i) ++ "\n"}, prg).stringcat
}

-- Evaluation environment: keeps a mapping between labels and (sub)programs
fun initEvalEnv (insns) {
  var map =
    fix (fun (rec) {
          fun ([m, insns]) {
            case insns of
              {}               -> m
            | LABEL (lab) : tl -> rec ([addMap (m, lab, insns), tl])
            | _ : tl           -> rec ([m, tl])
            esac
          }
        }) $ [emptyMap (compare), insns];
  
  [fun (l) {
     case findMap (map, l) of Some (insns) -> insns esac
  }]
}

-- Accessor function
fun fromLabel (env, lab) {
  env [0] (lab)
}

-- Stack machine interpreter. Takes an environment, an SM-configuration and a program,
-- returns a final configuration
fun eval (env, c@[stack, state, world], insns) {
  case insns of
    {} -> [stack, state, world]
    | start:end -> 
      case start of
        READ -> 
          case readWorld(world) of [inst, world] ->
            eval (env,[inst:stack, state, world], end) 
          esac
      | WRITE -> 
          case stack of inst:stack -> 
            eval(env,[stack, state, writeWorld(inst, world)], end) 
          esac
      | BINOP (op) -> 
          case stack of right:left:stack -> 
            var ans = case op of
              "+"  -> left + right
            | "-"  -> left - right
            | "*"  -> left * right
            | "/"  -> left / right
            | "!=" -> left != right
            | "&&" -> left && right
            | "!!" -> left !! right
            | "%"  -> left % right
            | "==" -> left == right
            | "<"  -> left < right
            | "<=" -> left <= right
            | ">"  -> left > right
            | ">=" -> left >= right
              esac;
            eval(env,[ans:stack, state, world], end) 
          esac
      | LD (varStr) -> 
          eval (env,[state (varStr):stack, state, world], end)
      | ST (varStr) -> 
          case stack of inst:stack -> 
            eval (env,[stack, state <- [varStr, inst], world], end) 
          esac
      | CONST (num) -> 
          eval (env,[num:stack, state, world], end)
      | LABEL (_) -> eval (env, [stack, state, world], end)
      | JMP (lab) -> eval (env, [stack, state, world], env.fromLabel (lab))
      | CJMP ("nz", lab) -> case stack of value : stack -> eval (env, [stack, state, world], if value != 0 then env.fromLabel (lab) else end fi) esac
      | CJMP ("z", lab) -> case stack of value : stack -> eval (env, [stack, state, world], if value == 0 then env.fromLabel (lab) else end fi) esac
      esac
  esac
}

-- Runs a stack machine for a given input and a given program, returns an output
public fun evalSM (input, insns) {
  eval (initEvalEnv (insns), [{}, emptyState, createWorld (input)], insns)[2].getOutput
}

-- Compilation environment: generates labels
fun makeCompEnv (n) {
  [fun () {
     [sprintf ("L%d", n), makeCompEnv (n+1)]
  }]
}

fun initCompEnv () {
  makeCompEnv (0)
}

-- Accessor function: generates one label
fun genLabel (env) {
  env [0] ()
}

-- Utility function: generates n labels
fun genLabels (env, n) {
  fun inner (env, n) {
    if n == 0
    then {env}
    else case env.genLabel of
           [lab, env] -> lab : inner (env, n-1)
         esac
    fi
  }

  listArray (inner (env, n))
}

-- Compiles an expression into a stack machine code.
-- Takes an expression, returns a list of stack machine instructions
fun compileExpr (expr) {
  case expr of
    Var (name) -> singletonBuffer (LD (name))
  | Const (n) -> singletonBuffer (CONST (n))
  | Binop (op, l, r) -> compileExpr (l) <+> compileExpr (r) <+ BINOP (op)
  esac
}

-- Compiles a statement into a stack machine code.
-- Takes a statement, returns a list of stack machine
-- instructions.
public fun compileSM (stmt) {
  -- Generates a buffer with an optional label
  --    lab     --- the name of the label
  --    labUsed --- a flag if the lebel is needed
  fun label (lab, labUsed) {
    if labUsed
    then singletonBuffer (LABEL (lab))
    else emptyBuffer ()
    fi
  }

  -- Generates SM code for a given statement
  --    lab  --- a label th go to after the statement is executed
  --    env  --- compilation environment
  --    stmt --- a statement to generate SM code for
  -- Returns a triple:
  --    boolean flag indicating if "lab" was used
  --    updated environment
  --    code buffer  
  fun compile (lab, env, stmt) {
    case stmt of
      Skip           -> [false, env, emptyBuffer ()]
    | Read  (x)      -> [false, env, singletonBuffer (READ) <+ ST (x)]
    | Write (e)      -> [false, env, compileExpr (e) <+ WRITE]
    | Assn  (x, e)   -> [false, env, compileExpr (e) <+ ST (x)]
    | Seq   (s1, s2) ->
        case env.genLabel of
          [s2Lab, env] ->
             case compile (s2Lab, env, s1) of
               [s2LabUsed, env, s1Code] ->
                  case compile (lab, env, s2) of
                    [labUsed, env, s2Code] -> [labUsed, env, s1Code <+> label (s2Lab, s2LabUsed) <+> s2Code]
                  esac
             esac
        esac
    | If (c, o, e) ->
        case env.genLabel of
          [l, env] ->
            case compile(lab, env, o) of 
              [r, env, tCode] ->
                case compile(lab, env, e) of
                 [r, env, eCode] -> [true, env, compileExpr(c) <+ 
                  CJMP ("z", l) <+> tCode <+ JMP (lab) <+ LABEL (l) <+> eCode ]
                esac
            esac
        esac
    | While (a, b) ->
        case env.genLabels (2) of
          [bCase, aCase, env] ->
            case compile(aCase, env, b) of 
              [r, env, bc] -> [false, env, singletonBuffer (JMP (aCase)) <+ 
                LABEL (bCase) <+> bc <+ LABEL (aCase) <+> compileExpr(a) <+ CJMP("nz", bCase)]
            esac
        esac
    | DoWhile (b, c) ->
        case env.genLabels (2) of
          [bCase, aCase, env] ->
            case compile(aCase, env, b) of 
              [r, env, bc] -> [false, env, singletonBuffer (LABEL (bCase)) <+> 
                bc <+ LABEL (aCase) <+> compileExpr(c) <+ CJMP("nz", bCase)]
            esac
        esac
    esac
  }  

  case initCompEnv ().genLabel of
    [endLab, env] ->
       case compile (endLab, env, stmt) of
         [endLabUsed, _, code] -> getBuffer $  code <+> label (endLab, endLabUsed)
       esac
  esac
}
